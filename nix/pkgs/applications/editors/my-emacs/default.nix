## Generate a wrapped emacs with all required libraries and executables in the
## path
##
## "all" here means, anything required by the configuration generated by the
## emacs-config derivation in this monorepo
##
## Since some packages carry some heavy dependencies with them, you can
## denylist them if you need to generate an emacs but don't want to spend the
## time on waiting for a plethora of things to download (check emacs-config.nix)
{
  aspell-wrapped,
  colorThemeSolarized,
  company,
  coreutils,
  emacs-config,
  emacs-config-options,
  emacsWithPackages,
  erlangMode,
  flycheck-haskell,
  gawk,
  ghc,
  git,
  gnused,
  groovy-mode,
  haskell-mode,
  helm,
  helm-ls-git,
  helm-lsp,
  helm-org,
  htmlize,
  lib,
  lsp-haskell,
  lsp-ui,
  markdown-mode,
  merlin,
  nix-mode,
  ocp-indent,
  projectile,
  sbt,
  scalaMode2,
  silver-searcher,
  terraform-mode,
  tuareg,
  utop,
  yaml-mode,
  yasnippet,
}:
let
  # FIXME remove comments and unused dependencies
  deps = {
    "haskell" = [ helm-lsp lsp-haskell lsp-ui flycheck-haskell company ];
    "ocaml"   = [ merlin ocp-indent tuareg utop ];
    "erlang"  = [ erlangMode ];
  };

  mode-deps = mode:
    if   lib.elem mode emacs-config-options.denylisted-modes
    then []
    else builtins.getAttr mode deps;

  # Cheap or always needed, dependencies, just install them no matter what
  hardcoded-deps = [
    aspell-wrapped
    colorThemeSolarized
    emacs-config
    git
    groovy-mode
    helm
    helm-ls-git
    helm-org
    htmlize
    markdown-mode
    nix-mode
    projectile
    silver-searcher
    terraform-mode
    yaml-mode
    yasnippet
  ];

  dep-packages = lib.concatMap mode-deps [
    "erlang"
    "haskell"
    "ocaml"
  ];
in
emacsWithPackages (hardcoded-deps ++ dep-packages)
