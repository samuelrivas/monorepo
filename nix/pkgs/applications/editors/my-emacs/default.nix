## Generate a wrapped emacs with all required libraries and executables in the
## path
##
## "all" here means, anything required by the configuration generated by the
## emacs-config derivation in this monorepo
##
## Since some packages carry some heavy dependencies with them, you can
## denylist them if you need to generate an emacs but don't want to spend the
## time on waiting for a plethora of things to download (check emacs-config.nix)
{
  aspell-wrapped,
  colorThemeSolarized,
  company,
  eglot,
  emacs-config,
  emacs-config-options,
  emacsWithPackages,
  erlangMode,
  flycheck-haskell,
  git,
  groovy-mode,
  haskell-mode,
  helm,
  helm-ls-git,
  helm-org,
  htmlize,
  lib,
  markdown-mode,
  nix-mode,
  projectile,
  silver-searcher,
  stylish-haskell,
  terraform-mode,
  yaml-mode,
  yasnippet,
}:
let
  deps = {
    "haskell" = [ flycheck-haskell stylish-haskell ];
    "erlang"  = [ erlangMode ];
  };

  mode-deps = mode:
    if   lib.elem mode emacs-config-options.denylisted-modes
    then []
    else builtins.getAttr mode deps;

  # Cheap or always needed, dependencies, just install them no matter what
  hardcoded-deps = [
    aspell-wrapped
    colorThemeSolarized
    company
    eglot
    emacs-config
    git
    groovy-mode
    helm
    helm-ls-git
    helm-org
    htmlize
    markdown-mode
    nix-mode
    projectile
    silver-searcher
    terraform-mode
    yaml-mode
    yasnippet
  ];

  dep-packages = lib.concatMap mode-deps [
    "erlang"
    "haskell"
  ];
in
emacsWithPackages (hardcoded-deps ++ dep-packages)
